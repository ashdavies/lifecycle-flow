buildscript { handler ->
  apply from: file('gradle/repositories.gradle'), to: handler

  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.0-alpha08'
    classpath 'com.google.gms:google-services:4.3.1'
    classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.5.1.0'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41'
  }
}

apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: file('gradle/git-release-notes.gradle')
apply from: file('gradle/gradle-mvn-push.gradle')
apply from: file('gradle/repositories.gradle')

apply plugin: 'de.mannodermaus.android-junit5'

android {
  compileSdkVersion 29

  defaultConfig {
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    minSdkVersion 14
    targetSdkVersion 29
  }

  sourceSets {
    main.java.srcDirs 'src/main/kotlin'
    test.java.srcDirs 'src/test/kotlin'
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    freeCompilerArgs += [
        '-Xuse-experimental=kotlin.contracts.ExperimentalContracts',
        '-Xuse-experimental=kotlin.Experimental',
        '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi'
    ]

    jvmTarget = JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
  implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.50'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

  testImplementation 'com.github.ashdavies:lifecycle-testing:0.2.1'
  testImplementation 'com.google.truth:truth:1.0'
  testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.2'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.5.0'
}

tasks
    .withType(Javadoc)
    .all { enabled = false }
